package ptbm.models

import static extension ptbm.StaticUtils.*
import ptbm.VariationalReal

/**
 * Good example where naive MF does not work well. 
 * Lambda fixed ref = 0.7
 * Lambda variational = 2.9
 * 
 * Can show the point very well with N = 2..
 * Then 5 round trips for variational, 98 for fixed...
 * See outputFolder : /Users/bouchard/w/ptanalysis/results/all/2022-04-16-13-59-21-DLuZhDvl.exec
 * 
 * but even better would be an example where one end point has zero round trips
 */
model EightSchools {
  param GlobalDataSource data
  param Plate<String> schools
  param Boolean useTPrior ?: true
  
  random VariationalReal mu ?: unconstrainedLatentReal
  random VariationalReal tau ?: unconstrainedLatentReal
  random Plated<VariationalReal> thetas 
  random Plated<RealVar> ys
  random Plated<RealVar> sigmas
  
  laws {
    
    mu ~ Opt(Normal::distribution(0, 5 * 5))
    tau | RealDistribution prior = if (useTPrior) HalfStudentT::distribution(1, 5) else Normal::distribution(0, 5 * 5)
       ~ Opt(prior)
    
    for (Index<String> school : schools.indices) {
      thetas.get(school) | mu, tau ~ Opt(Normal::distribution(mu, tau.softplus))
      ys.get(school) | RealVar theta = thetas.get(school), RealVar sigma = sigmas.get(school) ~ Normal(theta, sigma * sigma)
    }
  }
}