package ptbm.models

import static extension ptbm.StaticUtils.*
import ptbm.VariationalReal

model MRNATransfection {
  
  param GlobalDataSource data
  
  random VariationalReal
    km0 ?: unconstrainedLatentReal,
    delta ?: unconstrainedLatentReal,
    beta ?: unconstrainedLatentReal,
    t0 ?: unconstrainedLatentReal,
    sigma ?: unconstrainedLatentReal
    
  param Plate<Double> times 
  
  random Plated<RealVar> observations 
  
  laws {
    
    t0 ~ Opt(Normal::distribution(0, 1000)) 
    km0 ~ Opt(Normal::distribution(0, 1000))
    beta ~ Opt(Normal::distribution(0, 1000))
    delta ~ Opt(Normal::distribution(0, 1000))
    sigma ~ Opt(Normal::distribution(0, 1000))
    
    for (Index<Double> time : times.indices) {
      observations.get(time) | Double t = time.key, km0, delta, beta, t0, sigma ~ Normal({
        val mean = km0.softplus / (delta.softplus - beta.softplus) * (1.0 - exp(-(delta.softplus - beta.softplus) * (t - t0.softplus))) * exp(-beta.softplus*(t - t0.softplus))
        if (mean.isNaN || mean == Double.NEGATIVE_INFINITY || mean == Double.POSITIVE_INFINITY)
          return 10_000 // hack: priors too vague but keeping them for replication
        return mean
      }, 
      sigma.softplus * sigma.softplus)   
    }
    
  }
}